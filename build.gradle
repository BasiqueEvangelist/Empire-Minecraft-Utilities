// Inspired in part by the buildscripts of FabricMC/fabric

plugins {
  id "fabric-loom" version "0.9.+"
  id "com.modrinth.minotaur" version "1.2.+"
  id "org.cadixdev.licenser" version "0.6.+"
  id "io.github.juuxel.loom-quiltflower" version "1.2.+"
}

String mod_version = "2.3.0-XAERO.12"
String release_title = null
String changeLog = null

String mc_version = "1.17.1"
String mc_version_major = "1.17"
String yarn_mappings = "43"       // https://fabricmc.net/versions.html
String loader_version = "0.11.6"  // https://fabricmc.net/versions.html
String fabric_version = "0.39.1"  // https://fabricmc.net/versions.html
String modmenu_version = "2.0.5"  // https://modrinth.com/mod/modmenu/versions
String lombok_version = "1.18.20" // https://mvnrepository.com/artifact/org.projectlombok/lombok
String voxel_version = "1.10.15"  // https://www.curseforge.com/minecraft/mc-mods/voxelmap/files/all
String xmm_version = "21.16.0"    // https://www.curseforge.com/minecraft/mc-mods/xaeros-minimap/files/all
String xwm_version = "1.16.1"     // https://www.curseforge.com/minecraft/mc-mods/xaeros-world-map/files/all

archivesBaseName = "emcutils-${mc_version}"
version = mod_version

repositories {
  maven { url "https://maven.terraformersmc.com" }
  maven { url "https://maven.waffle.coffee" }
}

loom { accessWidenerPath = file("src/main/resources/emcutils.aw") }

Set<String> apiModules = [
        "fabric-api-base",
        "fabric-item-api-v1",
        "fabric-networking-api-v1"
]

dependencies {
  minecraft "com.mojang:minecraft:${mc_version}"
  mappings "net.fabricmc:yarn:${mc_version}+build.${yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${loader_version}"
  apiModules.forEach { include(modImplementation(fabricApi.module(it, "${fabric_version}+${mc_version_major}"))) }

  modImplementation "com.terraformersmc:modmenu:${modmenu_version}"

  implementation "org.projectlombok:lombok:${lombok_version}"
  annotationProcessor "org.projectlombok:lombok:${lombok_version}"
  testImplementation "org.projectlombok:lombok:${lombok_version}"
  testAnnotationProcessor "org.projectlombok:lombok:${lombok_version}"

  modApi "com.mamiyaotaru:voxelmap:${voxel_version}+${mc_version_major}"
  modApi "xaero:minimap:${xmm_version}+${mc_version_major}"
  modApi "xaero:map:${xwm_version}+${mc_version_major}"
}

processResources {
  inputs.property "version", project.version
  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }

import com.modrinth.minotaur.TaskModrinthUpload

task modrinth(type: TaskModrinthUpload) {
  onlyIf { System.getenv().MODRINTH_TOKEN }
  token = System.getenv().MODRINTH_TOKEN
  projectId = "QYTT62S0"
  versionNumber = version
  versionName = release_title
  changelog = changeLog
  uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
  addGameVersion(mc_version)
}
