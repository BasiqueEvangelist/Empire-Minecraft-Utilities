plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.10.0-SNAPSHOT" apply false
    id "com.modrinth.minotaur" version "1.2.+"
    id "org.cadixdev.licenser" version "0.6.+"
    id "io.github.juuxel.loom-quiltflower-mini" version "1.+" apply false
}


String release_title = "Fix many bugs and introduce a couple more"
String changeLog = "There are still many known bugs! Be patient, I am working on fixing them, but wanted to get this hotfix out.\n" +
        "- Hotbar placement is strange\n" +
        "- Map name detection has been temporarily removed for Xaero's users\n" +
        "- Other things I'm forgetting\n" +
        "Hopefully nothing too game-breaking! :P"

String yarn_mappings   = "63"           // https://fabricmc.net/versions.html
String lombok_version  = "1.18.22"      // https://mvnrepository.com/artifact/org.projectlombok/lombok



architectury {
    minecraft = mc_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "io.github.juuxel.loom-quiltflower-mini"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${mc_version}"
        mappings "net.fabricmc:yarn:${mc_version}+build.${yarn_mappings}:v2"

        implementation annotationProcessor("org.projectlombok:lombok:${lombok_version}")
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = "emcutils"
    version = rootProject.mc_version + "-" + mod_version + System.getenv().SNAPSHOT

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven {
            url "https://maven.terraformersmc.com"
            content { includeGroup "com.terraformersmc" }
        }
        maven {
            url "https://maven.waffle.coffee/releases"
            content {
                includeGroup "com.mamiyaotaru"
                includeGroup "xaero"
            }
        }
    }

    // Comment this line out if you have good internet
    idea { module { downloadJavadoc = false } }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 16
    }

    java {
        withSourcesJar()
    }
}