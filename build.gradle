//file:noinspection UnnecessaryQualifiedReference
//file:noinspection GroovyAssignabilityCheck
plugins {
  id "fabric-loom" version "0.10.+"
  id "com.modrinth.minotaur" version "1.2.+"
  id "org.cadixdev.licenser" version "0.6.+"
  id "io.github.juuxel.loom-quiltflower-mini" version "1.+"
  id "maven-publish"
}

String mod_version = loom.modVersion
String release_title = null
String changeLog = null

/// Minecraft & Fabric ///
String mc_version = "1.17.1"
String mc_version_major = "1.17"
String yarn_mappings   = "63"           // https://fabricmc.net/versions.html
String loader_version  = "0.12.4"       // https://fabricmc.net/versions.html
String fabric_version  = "0.41.3"       // https://fabricmc.net/versions.html

/// Useful annotations ///
String lombok_version  = "1.18.22"      // https://mvnrepository.com/artifact/org.projectlombok/lombok

/// Compat ///
String modmenu_version = "2.0.14"       // https://modrinth.com/mod/modmenu/versions
String voxel_version   = "1.10.15"      // https://www.curseforge.com/minecraft/mc-mods/voxelmap/files/all
String xmm_version     = "21.20.0"      // https://www.curseforge.com/minecraft/mc-mods/xaeros-minimap/files/all
String xwm_version     = "1.18.0"       // https://www.curseforge.com/minecraft/mc-mods/xaeros-world-map/files/all
String gbl_version     = "1.3.4"        // https://minecraft.guntram.de/maven/de/guntram/mcmod/GBfabrictools/maven-metadata.xml
String crowdin_version = "1.3"          // https://minecraft.guntram.de/maven/de/guntram/mcmod/crowdin-translate/maven-metadata.xml
String echests_version = "0.36.1-1.7.0" // https://api.modrinth.com/maven/maven/modrinth/easierchests/maven-metadata.xml

archivesBaseName = "emcutils"
version = mc_version + "-" + mod_version + System.getenv().SNAPSHOT

repositories {
  maven {
    url "https://maven.terraformersmc.com"
    content { includeGroup "com.terraformersmc" }
  }
  maven {
    url "https://maven.waffle.coffee/releases"
    content {
      includeGroup "com.mamiyaotaru"
      includeGroup "xaero"
    }
  }
  maven {
    url "https://api.modrinth.com/maven"
    content { includeGroup "maven.modrinth" }
  }
  maven {
    url "https://minecraft.guntram.de/maven/"
    content { includeGroup "de.guntram.mcmod" }
  }
}

// Comment this line out if you have good internet
idea { module { downloadJavadoc = false } }

loom { accessWidenerPath = file("src/main/resources/emcutils.tsv") }

dependencies {
  /// Minecraft & Fabric ///
  minecraft "com.mojang:minecraft:${mc_version}"
  mappings "net.fabricmc:yarn:${mc_version}+build.${yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${loader_version}"
  Set<String> apiModules = [ "api-base", "item-api-v1", "networking-api-v1" ]
  apiModules.forEach {
    include(modImplementation(fabricApi.module("fabric-$it", "${fabric_version}+${mc_version_major}")))
  }

  /// Useful annotations ///
  implementation annotationProcessor("org.projectlombok:lombok:${lombok_version}")

  /// Compat ///

  // Mod Menu
  modImplementation ("com.terraformersmc:modmenu:${modmenu_version}") {
    exclude group: "net.fabricmc.fabric-api"
    exclude group: "net.fabricmc"
  }

  // Map mods
  modApi "com.mamiyaotaru:voxelmap:${voxel_version}+${mc_version_major}"
  modApi "xaero:minimap:${xmm_version}+${mc_version_major}"
  modApi "xaero:map:${xwm_version}+${mc_version_major}"

  // EasierChests
  modApi("de.guntram.mcmod:GBfabrictools:${gbl_version}+${mc_version_major}") {
    exclude group: "net.fabricmc.fabric-api"
    exclude group: "net.fabricmc"
  }
  modApi "de.guntram.mcmod:crowdin-translate:${crowdin_version}+${mc_version_major}"
  modApi "maven.modrinth:easierchests:${mc_version}-fabric${echests_version}"
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }

task modrinth(type: com.modrinth.minotaur.TaskModrinthUpload) {
  onlyIf { System.getenv().MODRINTH_TOKEN }
  token = System.getenv().MODRINTH_TOKEN
  projectId = "QYTT62S0"
  versionNumber = mod_version
  versionName = release_title
  changelog = changeLog
  uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
  addGameVersion(mc_version)
}

java { withSourcesJar() }

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'coffee.waffle'
      artifactId = archivesBaseName
      version = mod_version + "+" + mc_version + System.getenv().SNAPSHOT
      artifact(remapJar) { builtBy remapJar }
      artifact(sourcesJar) { builtBy remapSourcesJar }
    }
  }
  repositories {
    maven {
      name = "emmavenReleases"
      url = "https://maven.waffle.coffee/releases"
      credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
      }
      authentication { basic(BasicAuthentication) }
    }
    maven {
      name = "emmavenSnapshots"
      url = "https://maven.waffle.coffee/snapshots"
      credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
      }
      authentication { basic(BasicAuthentication) }
    }
  }
}