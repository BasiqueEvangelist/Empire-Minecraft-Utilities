plugins {
  id "fabric-loom" version "0.6-SNAPSHOT"
  id "com.modrinth.minotaur" version "1.2.1"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = "${project.archives_base_name}-${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

repositories {
  maven { url "https://maven.terraformersmc.com/" }
  maven { url "https://maven.shedaniel.me/" }
  maven {
    url "https://www.cursemaven.com"
    content { includeGroup "curse.maven" }
  }
}

minecraft {
  accessWidener = file("src/main/resources/emcutils.accesswidener")
}

dependencies {
  // To change the versions see the gradle.properties file
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
  modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}" // Fabric API

  implementation "org.projectlombok:lombok:${project.lombok_version}"
  annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
  testImplementation "org.projectlombok:lombok:${project.lombok_version}"
  testAnnotationProcessor "org.projectlombok:lombok:${project.lombok_version}"

  // Minimaps
  modImplementation "curse.maven:voxelmap-225179:3197679"
  //modImplementation "curse.maven:xaeroworldmap-317780:3222863"
  //modImplementation "curse.maven:xaerominimap-263420:3222851"

  // Mod Menu & config integration
  modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
  modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") { exclude(group: "net.fabricmc.fabric-api") }
  include "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}"
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  def targetVersion = 8
  if (JavaVersion.current().isJava9Compatible()) {
    it.options.release.set(targetVersion)
  }
}

java { withSourcesJar() }

jar {
  from("LICENSE") {
    rename { "${it}_${project.archivesBaseName}" }
  }
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth(type: TaskModrinthUpload) {
  onlyIf { System.getenv("MODRINTH_TOKEN") }
  token = System.getenv("MODRINTH_TOKEN")
  projectId = 'QYTT62S0'
  versionNumber = '1.8.2'
  uploadFile = jar.outputs.getFiles().asPath
  addGameVersion('1.16.5')
  addLoader('fabric')
}
