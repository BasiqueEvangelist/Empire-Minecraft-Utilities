plugins {
  id "fabric-loom" version "0.9-SNAPSHOT"
  id 'io.github.juuxel.loom-quiltflower' version '1.1.3'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "emcutils-${project.minecraft_version}"
version = project.mod_version

repositories {
  maven { url "https://api.modrinth.com/maven" }
  maven { url "https://maven.shedaniel.me" }
  maven { url "https://www.cursemaven.com" }
}

loom { accessWidenerPath = file("src/main/resources/emcutils.aw") }

Set<String> apiModules = [
        "fabric-api-base",
        "fabric-item-api-v1",
        "fabric-networking-api-v1"
]

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
  apiModules.forEach { include(modImplementation(fabricApi.module(it, fabric_version))) }

  modImplementation "maven.modrinth:modmenu:${project.modmenu_version}"

  implementation "org.projectlombok:lombok:${project.lombok_version}"
  annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
  testImplementation "org.projectlombok:lombok:${project.lombok_version}"
  testAnnotationProcessor "org.projectlombok:lombok:${project.lombok_version}"

  modImplementation "curse.maven:voxelmap-225179:3345206"
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }
