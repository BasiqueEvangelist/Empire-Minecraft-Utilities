//file:noinspection UnnecessaryQualifiedReference
plugins {
  id "fabric-loom" version "0.10.+"
  id "com.modrinth.minotaur" version "1.2.+"
  id "org.cadixdev.licenser" version "0.6.+"
  id "io.github.juuxel.loom-quiltflower" version "1.3.+"
}

String mod_version = loom.modVersion
String release_title = null
String changeLog = null

String mc_version = "21w37a"
String mc_version_major = "1.18"
String yarn_mappings = "10"       // https://fabricmc.net/versions.html
String loader_version = "0.11.7"  // https://fabricmc.net/versions.html
String fabric_version = "0.40.3"  // https://fabricmc.net/versions.html
String modmenu_version = "2.1.0"  // https://modrinth.com/mod/modmenu/versions
String lombok_version = "1.18.20" // https://mvnrepository.com/artifact/org.projectlombok/lombok
String voxel_version = "1.10.15"  // https://www.curseforge.com/minecraft/mc-mods/voxelmap/files/all
String xmm_version = "21.16.0"    // https://www.curseforge.com/minecraft/mc-mods/xaeros-minimap/files/all
String xwm_version = "1.17.0.1"   // https://www.curseforge.com/minecraft/mc-mods/xaeros-world-map/files/all

archivesBaseName = "emcutils"
version = mc_version + "-" + mod_version

repositories {
  maven {
    url "https://maven.terraformersmc.com"
    content { includeGroup "com.terraformersmc" }
  }
  maven {
    url "https://maven.waffle.coffee"
    content {
      includeGroup "com.mamiyaotaru"
      includeGroup "xaero"
    }
  }
}

loom { accessWidenerPath = file("src/main/resources/emcutils.aw") }

Set<String> apiModules = [
        "api-base",
        "item-api-v1",
        "networking-api-v1"
]

dependencies {
  minecraft "com.mojang:minecraft:${mc_version}"
  mappings "net.fabricmc:yarn:${mc_version}+build.${yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${loader_version}"
  apiModules.forEach {
    include modImplementation(fabricApi.module("fabric-$it", "${fabric_version}+${mc_version_major}"))
  }

  modImplementation "com.terraformersmc:modmenu:${modmenu_version}"

  implementation annotationProcessor("org.projectlombok:lombok:${lombok_version}")

  modApi "com.mamiyaotaru:voxelmap:${voxel_version}+1.17" //${mc_version_major}"
  modApi "xaero:minimap:${xmm_version}+1.17" //${mc_version_major}"
  modApi "xaero:map:${xwm_version}+1.17" //${mc_version_major}"
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }

task modrinth(type: com.modrinth.minotaur.TaskModrinthUpload) {
  onlyIf { System.getenv().MODRINTH_TOKEN }
  token = System.getenv().MODRINTH_TOKEN
  projectId = "QYTT62S0"
  versionNumber = mod_version
  versionName = release_title
  changelog = changeLog
  uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
  addGameVersion(mc_version)
}
