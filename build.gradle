plugins { id "fabric-loom" version "0.8-SNAPSHOT" }

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "${project.archives_base_name}-${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

repositories {
  maven { url "https://maven.terraformersmc.com/" }
  maven { url "https://maven.shedaniel.me/" }
  maven {
    url "https://www.cursemaven.com"
    content { includeGroup "curse.maven" }
  }
}

minecraft {
  accessWidener = file("src/main/resources/emcutils.accesswidener")
}

Set<String> apiModules = [
        "fabric-api-base",
        "fabric-item-api-v1",
        "fabric-lifecycle-events-v1",
        "fabric-networking-api-v1"
]

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.minecraft_version}+build.${project.yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
  apiModules.forEach {include(modImplementation(fabricApi.module(it, fabric_version)))}

  implementation "org.projectlombok:lombok:${project.lombok_version}"
  annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"
  testImplementation "org.projectlombok:lombok:${project.lombok_version}"
  testAnnotationProcessor "org.projectlombok:lombok:${project.lombok_version}"

  // Minimaps
  modImplementation "curse.maven:voxelmap-225179:3345206"
  //modImplementation "curse.maven:xaeroworldmap-317780:3222863"
  //modImplementation "curse.maven:xaerominimap-263420:3222851"

  // Mod Menu & config integration
  modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
  modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") { exclude(group: "net.fabricmc.fabric-api") }
  include "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}"
}

processResources {
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

java { withSourcesJar() }

jar { from("LICENSE") { rename { "${it}_${project.archivesBaseName}" } } }