// Inspired in part by the buildscripts of FabricMC/fabric

plugins {
  id "fabric-loom" version "0.9.+"
  id "com.modrinth.minotaur" version "1.2.+"
  id "org.cadixdev.licenser" version "0.6.+"
  id "io.github.juuxel.loom-quiltflower" version "1.2.+"
}

String mod_version = "2.2.1-SNAPSHOT"
String release_title = null
String changeLog = null

String minecraft_version = "1.17.1"   // https://fabricmc.net/versions.html
String yarn_mappings = "43"           // https://fabricmc.net/versions.html
String loader_version = "0.11.6"      // https://fabricmc.net/versions.html
String fabric_version = "0.39.1+1.17" // https://fabricmc.net/versions.html
String modmenu_version = "2.0.5"      // https://modrinth.com/mod/modmenu/versions
String lombok_version = "1.18.20"     // https://mvnrepository.com/artifact/org.projectlombok/lombok

archivesBaseName = "emcutils-${minecraft_version}"
version = mod_version

repositories {
  maven { url "https://maven.terraformersmc.com" }
  maven { url "https://www.cursemaven.com" }
}

loom { accessWidenerPath = file("src/main/resources/emcutils.aw") }

Set<String> apiModules = [
        "fabric-api-base",
        "fabric-item-api-v1",
        "fabric-networking-api-v1"
]

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  mappings "net.fabricmc:yarn:${minecraft_version}+build.${yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${loader_version}"
  apiModules.forEach { include(modImplementation(fabricApi.module(it, fabric_version))) }

  modImplementation "com.terraformersmc:modmenu:${modmenu_version}"

  implementation "org.projectlombok:lombok:${lombok_version}"
  annotationProcessor "org.projectlombok:lombok:${lombok_version}"
  testImplementation "org.projectlombok:lombok:${lombok_version}"
  testAnnotationProcessor "org.projectlombok:lombok:${lombok_version}"

  modImplementation "curse.maven:voxelmap-225179:3345206"
}

processResources {
  inputs.property "version", project.version
  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }

import com.modrinth.minotaur.TaskModrinthUpload

task modrinth(type: TaskModrinthUpload) {
  onlyIf { System.getenv().MODRINTH_TOKEN }
  token = System.getenv().MODRINTH_TOKEN
  projectId = "QYTT62S0"
  versionNumber = version
  versionName = release_title
  changelog = changeLog
  uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
  addGameVersion(minecraft_version)
}
